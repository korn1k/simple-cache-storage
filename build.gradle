plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'application'
}

group = 'com.simplecachestorage'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.42.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.19.0'
    implementation 'io.grpc:grpc-netty-shaded:1.42.1'
    implementation 'io.grpc:grpc-protobuf:1.42.1'
    implementation 'io.grpc:grpc-stub:1.42.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(23))
    }
}

application {
    mainClass = 'com.simplecachestorage.Main'
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.simplecachestorage.client.KeyValueStoreClient'
    standardInput = System.in
    standardOutput = System.out
    errorOutput = System.err
}